plugins {
    id 'org.jetbrains.intellij' version '0.4.15'
}

group 'org.endpointdeps'
version pluginVersion

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version ideaVersion

    updateSinceUntilBuild = false // disable building for each major version

    def versionParts = ideaVersion.replace('IU-', '').split('\\.')
    if ((Integer.parseInt(versionParts[0]) == 2019 && Integer.parseInt(versionParts[1]) >= 2) || Integer.parseInt(versionParts[0]) > 2019) {
        /* since 2019.2 Java is a separate plugin, hence we have to specify it implicitly */
        plugins = [
                'java',
                "com.jetbrains.php:${phpPluginVersion}",
                'properties',
                "PsiViewer:${psiViewerPluginVersion}",
        ]
    } else {
        plugins = [
                "com.jetbrains.php:${phpPluginVersion}",
                'properties',
                "PsiViewer:${psiViewerPluginVersion}",
        ]
    }
}

patchPluginXml {
    sinceBuild        = "162.1889"
    untilBuild        = ""
    changeNotes       = htmlFixer('src/main/resources/META-INF/change-notes.html')
    pluginDescription = htmlFixer('src/main/resources/META-INF/description.html')
}